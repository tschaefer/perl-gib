#!/usr/bin/env perl

use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/../lib";

use Getopt::Long qw(:config require_order);
use Path::Tiny;
use Pod::Usage;
use Term::ANSIColor;
use Time::HiRes qw(gettimeofday);
use Try::Tiny;

use Perl::Gib;

$Term::ANSIColor::AUTORESET = 1;

no warnings "uninitialized";

my %opts;

sub print_usage {
    return pod2usage( -exitval => 255, -verbose => 0 );
}

sub print_help {
    return pod2usage(
        -exitval  => 0,
        -verbose  => 99,
        -sections => 'SYNOPSIS|OPTIONS|PARAMETERS',
    );
}

sub print_man {
    return pod2usage( -exitval => 0, -verbose => 2 );
}

sub print_version {
    printf "perlgib %s\n", $Perl::Gib::VERSION;

    return;
}

sub new_perlgib {
    while ( my ( $key, $value ) = each %opts ) {
        delete $opts{$key} if ( !defined $value );
    }

    return Perl::Gib->new(%opts);
}

sub do_doc {
    GetOptions(
        "output-path=s"          => \$opts{'output_path'},
        "output-format=s"        => \$opts{'output_format'},
        "document-private-items" => \$opts{'document_private_items'},
        "document-ignored-items" => \$opts{'document_ignored_items'},
        "no-html-index"          => \$opts{'no_html_index'},
    ) or return print_usage();

    my $perlgib = new_perlgib();

    my $format  = $opts{'output_format'} || 'html';
    my %formats = (
        'html'     => 1,
        'markdown' => 1,
        'all'      => 1,
    );
    print_usage() if ( !$formats{$format} );

    $perlgib->html()     if ( $format =~ /html|all/ );
    $perlgib->markdown() if ( $format =~ /markdown|all/ );

    return;
}

sub do_test {
    my $perlgib = new_perlgib();

    $perlgib->test();

    return;
}

sub do_action {
    my ( $method, $info ) = @_;

    my $lib = $opts{'library_name'};
    if ( !$lib ) {
        $lib =
          $opts{'library_path'}
          ? path( $opts{'library_path'} )
          : path('lib');
        $lib = $lib->absolute->realpath->stringify;
    }

    printf "%s (%s)\n", colored( $info, 'green' ), $lib;
    my $start = Time::HiRes::gettimeofday();

    my $rc = try {
        &$method();
        return 0;
    }
    catch {
        printf {*STDERR} "%s\n", ( split / at/ )[0];
        return 1;
    };

    my $stop = Time::HiRes::gettimeofday();
    printf "%s in %.2fs\n", colored( 'Finished', $rc ? 'red' : 'green' ),
      $stop - $start;

    return $rc;
}

sub run {
    GetOptions(
        "library-path=s" => \$opts{'library_path'},
        "library-name=s" => \$opts{'library_name'},
        "help|h"         => \$opts{'help'},
        "man|m"          => \$opts{'man'},
        "version|v"      => \$opts{'version'},
    ) or return print_usage();

    return print_help()    if ( $opts{'help'} );
    return print_man()     if ( $opts{'man'} );
    return print_version() if ( $opts{'version'} );

    my %METHOD = (
        doc  => [ \&do_doc,  'Documenting' ],
        test => [ \&do_test, 'Testing' ],
    );

    my $action = shift @ARGV;
    return print_usage() if ( !$METHOD{$action} );

    my ( $method, $info ) = @{ $METHOD{$action} };
    return do_action( $method, $info );
}

exit run();

__END__

=encoding utf8

=head1 NAME

perlgib - Perl's alternative documentation and test manager.

=head1 SYNOPSIS

perlgib --help|-h | --man|-m | --version|-v

perlgib [OPTIONS] doc | test [OPTIONS]

=head1 OPTIONS

=head2 base

=over 8

=item --help|-h

Print short usage help.

=item --man|-m

Print extended usage help.

=item --version|-v

Print version string.

=item --library-path PATH

Directory with documents (Perl modules, Markdown files) to process, default
lib in current working directory.

=item --library-name NAME

Library name.

=back

=head2 doc

Build library documentation.

=over 8

=item --output-path

Documentation output path, default doc in current working directory.

=item --output-format html|markdown|all

Documentation output format, default html.

=item --document-private-items

Document private items.

=item --document-ignored-items

Document ignored items (#[ignore(item)]).

=item --no-html-index

Prevent creating of html index.

=back

=head2 test

Execute documentation tests.

=head1 DESCRIPTION

perlgib is Perl's alternative documentation and test manager.

perlgib generates HTML and Markdown documentation and runs tests from Perl
code comment lines.

=cut
